<!--
/*************************************************************
  3D Graphics Programming
  Texture effect example with noise application. 
  Man-on-moon texture is public domain.
  (c) anssi.grohn at karelia.fi 2013
 *************************************************************/
 -->
<?doctype HTML>
<html>
<head>
<meta charset="utf-8">
<title>RGB Shift + Bad Reception + Scanline Effect</title>
<!-- include jQuery -->
<script src="../lib/jquery-1.9.1.min.js"></script>
<script src="../lib/three.js"></script>

<!-- fragment shader code -->
<script id="shader-fs" type="x-shader/x-fragment">
precision mediump float;

uniform sampler2D tex;
// uniform angle for sin (bad reception effect)
uniform float a;
varying vec2 vUV;

//
// Description : Array and textureless GLSL 2D simplex noise function.
// Author : Ian McEwan, Ashima Arts.
// Maintainer : ijm
// Lastmod : 20110822 (ijm)
// License : Copyright (C) 2011 Ashima Arts. All rights reserved.
// Distributed under the MIT License. See LICENSE file.
// https://github.com/ashima/webgl-noise
//

vec3 mod289(vec3 x) {
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec2 mod289(vec2 x) {
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec3 permute(vec3 x) {
  return mod289(((x*34.0)+1.0)*x);
}

float snoise(vec2 v)
  {
  const vec4 C = vec4(0.211324865405187, // (3.0-sqrt(3.0))/6.0
                      0.366025403784439, // 0.5*(sqrt(3.0)-1.0)
                     -0.577350269189626, // -1.0 + 2.0 * C.x
                      0.024390243902439); // 1.0 / 41.0
// First corner
  vec2 i = floor(v + dot(v, C.yy) );
  vec2 x0 = v - i + dot(i, C.xx);

// Other corners
  vec2 i1;
  //i1.x = step( x0.y, x0.x ); // x0.x > x0.y ? 1.0 : 0.0
  //i1.y = 1.0 - i1.x;
  i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
  // x0 = x0 - 0.0 + 0.0 * C.xx ;
  // x1 = x0 - i1 + 1.0 * C.xx ;
  // x2 = x0 - 1.0 + 2.0 * C.xx ;
  vec4 x12 = x0.xyxy + C.xxzz;
  x12.xy -= i1;

// Permutations
  i = mod289(i); // Avoid truncation effects in permutation
  vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 )) + i.x + vec3(0.0, i1.x, 1.0 ));

  vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy), dot(x12.zw,x12.zw)), 0.0);
  m = m*m ;
  m = m*m ;

// Gradients: 41 points uniformly over a line, mapped onto a diamond.
// The ring size 17*17 = 289 is close to a multiple of 41 (41*7 = 287)

  vec3 x = 2.0 * fract(p * C.www) - 1.0;
  vec3 h = abs(x) - 0.5;
  vec3 ox = floor(x + 0.5);
  vec3 a0 = x - ox;

// Normalise gradients implicitly by scaling m
// Approximation of: m *= inversesqrt( a0*a0 + h*h );
  m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );

// Compute final noise value at P
  vec3 g;
  g.x = a0.x * x0.x + h.x * x0.y;
  g.yz = a0.yz * x12.xz + h.yz * x12.yw;
  return 130.0 * dot(m, g);
}

void main() {
    
    float factor = clamp(mod(gl_FragCoord.y,4.0), 0.0,0.50);
    // This creates the bad reception effect:
    // texture coordinates are affected by fragment's vertical position 
    // and furthermore modified by sin function
    vec2 UV = vUV + vec2(factor*snoise(vUV+vec2(gl_FragCoord.y*sin(a*101.0),0.0))/50.0,0.0);
    // rgb shift, take several samples with slight offset
    vec4 s1 = texture2D(tex, UV);
    vec4 s2 = texture2D(tex, UV-vec2(0.012,0.0));
    vec4 s3 = texture2D(tex, UV+vec2(0.012,0.0));
    s2.r = 0.0; // remove red component 
    s3.g = 0.0; // remove green component 

    // combine color from different samples
    vec4 color = mix(s1, s2+s3, 0.4245);

    // scanline effect, every fourth line is slightly darker 
    color -= 0.25*clamp(mod(gl_FragCoord.y, 4.0)-2.0, 0.0,1.0);

    // final color
    gl_FragColor = color;

}
</script>

<!-- vertex shader code -->
<script id="shader-vs" type="x-shader/x-vertex">
varying vec2 vUV;
uniform vec2 offset;
void main()
{
    vUV  = uv+offset;
    gl_Position = projectionMatrix * modelViewMatrix * vec4(position,1.0);
}
</script>

<!-- include our lesson-based js -->

<script>
$(function(){
    var scene = new THREE.Scene(); 
    var camera = new THREE.PerspectiveCamera(75, window.innerWidth/window.innerHeight, 0.1, 1000); 
    var renderer = new THREE.WebGLRenderer(); 
    renderer.setSize(window.innerWidth, window.innerHeight); 
    $("#main").append(renderer.domElement); 
    var geometry = new THREE.Geometry(); 

    // simple geometry, only a quad
    geometry.vertices.push( new THREE.Vector3(-0.5, -0.5, 0.0),
                            new THREE.Vector3(0.5, -0.5, 0.0),
                            new THREE.Vector3(0.5, 0.5, 0.0),
                            new THREE.Vector3(-0.5, 0.5, 0.0));

    geometry.faces.push( new THREE.Face4(0,1,2,3) );
    geometry.faces[0].normal.set(0,0,1);
    geometry.faceVertexUvs[0].push ( [new THREE.Vector2(0.0, 0.0),
                                      new THREE.Vector2(1.0, 0.0),
                                      new THREE.Vector2(1.0, 1.0),
                                      new THREE.Vector2(0.0, 1.0)]);

    // the "happy" instructor 
    var t = THREE.ImageUtils.loadTexture("man-on-moon.png");
    t.wrapS = THREE.RepeatWrapping;

    var material = new THREE.ShaderMaterial({
        vertexShader: $("#shader-vs").text(),
        fragmentShader: $("#shader-fs").text(),
        uniforms: { 
            tex: { type: 't', value: t },
            a:   {type: 'f', value: 0.0 },
            offset: { type: 'v2', value: new THREE.Vector2(0.0,0.0)}
        }
    }); 
    
    var quad = new THREE.Mesh(geometry, material); 
    scene.add(quad); 
    camera.position.z = 1;
    material.uniforms.offset.value.x = 0.0;

    function render() { 
        requestAnimationFrame(render); 
	// add angle on every frame to get "jitters"
        material.uniforms.a.value += 0.01;
        if ( material.uniforms.a.value > Math.PI*2.0 ){
                material.uniforms.a.value -= Math.PI*2.0;        
        }
        renderer.render(scene, camera); 
    } 
    
    render();
});
</script>
</head>
<body >
<div id="title">RGB Shift + bad reception + scanline effects</div>
<div id="main">
</div>
</body>
</html>
